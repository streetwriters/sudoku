apply plugin: 'com.android.application'
import com.android.build.OutputFile

android {
    compileSdkVersion 31
    buildToolsVersion '31.0.0'
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_10
        targetCompatibility JavaVersion.VERSION_1_10

    }
    defaultConfig {
        applicationId "com.streetwriters.sudoku"
        minSdkVersion 21
        targetSdkVersion 31
        versionCode 8
        versionName "2.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        //multiDexEnabled true //important
    }

    splits {
        abi {
            reset()
            enable true
            universalApk false  // If true, also generate a universal APK
            include 'arm64-v8a', 'x86', 'armeabi-v7a', 'x86_64'
        }
    }

    buildTypes {
        release {

            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main {
            res.srcDirs =
                    [
                            'src/main/res/layouts/game-activity',
                            'src/main/res/layouts/help-activity',
                            'src/main/res/layouts/main-activity',
                            'src/main/res/layouts/settings-activity',
                            'src/main/res/layouts/main-fragments',
                            'src/main/res/layouts/stats-layouts',
                            'src/main/res/layouts/trophy-layouts',
                            'src/main/res/layouts/dialogs',
                            'src/main/res/layouts',
                            'src/main/res'
                    ]
        }
    }

    buildFeatures {
        viewBinding true
    }

    

    // applicationVariants are e.g. debug, release
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            // For each separate APK per architecture, set a unique version code as described here:
            // https://developer.android.com/studio/build/configure-apk-splits.html
            def versionCodes = ["armeabi-v7a": 1, "x86": 2, "arm64-v8a": 3, "x86_64": 4]
            def abi = output.getFilter(OutputFile.ABI)
            if (abi != null) {  // null for the universal-debug, universal-release variants
                output.versionCodeOverride =
                        versionCodes.get(abi) * 1048576 + defaultConfig.versionCode
            }

        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar','*.aar'])

    implementation 'androidx.appcompat:appcompat:1.4.2'
    implementation 'com.google.android.material:material:1.6.1'
    implementation 'com.github.prolificinteractive:material-calendarview:2.0.1'
    implementation 'com.jakewharton.threetenabp:threetenabp:1.1.1'
    implementation 'com.plattysoft.leonids:LeonidsLib:1.3.2'
    implementation 'androidx.preference:preference:1.2.0'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test:rules:1.4.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
}
